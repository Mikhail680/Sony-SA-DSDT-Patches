#Maintained by: RehabMan for: Laptop Patches
#graphics_INI-disable.txt

into method label POFF remove_entry;
into definitionblock code_regex . insert
begin
    Method (POFF, 0, NotSerialized)\n
    {\n
        \_SB.PCI0.PEG0.PEGP._OFF ()\n
    }\n
end;
    
#WAK patch
into method label _WAK code_regex ([\s\S]*) replace_matched
begin
POFF ()\n
\\_SB.PCI0.IGPU.DD02._BCM (BRTL)\n
%1
end;


#HDEF patch
into method label _WAK code_regex (Return\s)\(Package(\s\(0x02\)[^\)]*) replace_matched
begin
 Notify (\\_SB.PCI0.HDEF, 0x02)\n
%0
end;

#Patch IGPU
into method label _INI parent_adr 0x00020000 code_regex ISTP\s\(.* replace_matched
begin
POFF ()\n
ISTP (0x0A, Zero)\n
end;

# Return 0
into method label _BCM parent_label DD02 insert
begin
 Return (Zero)\n
end;

#Patch 1
into device label PEG0 code_regex (Name.*_PRW.*\n.*\n.*\n.*\n.*\}\)) replace_matched
begin
%0\n
    OperationRegion (RPCI, PCI_Config, Zero, 0xF0)\n
    Field (RPCI, DWordAcc, Lock, Preserve)\n
    {\n
                  Offset (0xB0),\n
          ASPM,   2,\n
              ,   2,\n
          LNKD, 1\n
    }\n
\n
    OperationRegion (RPCX, SystemMemory, XBAS, 0x8400)\n
    Field (RPCX, DWordAcc, NoLock, Preserve)\n
    {\n
              Offset (0x8214),\n
              Offset (0x8216),\n
          LNKS, 4\n
    }\n
end;

#Patch 2
into device label PEGP code_regex (Name.*_PRW.*\n.*\n.*\n.*\n.*\}\)) replace_matched
begin
%0\n

       Name (ELCT, Zero)\n
                Name (HVID, Zero)\n
                Name (HDID, Zero)\n
                OperationRegion (PCIS, PCI_Config, Zero, 0x0100)\n
                Field (PCIS, DWordAcc, Lock, Preserve)\n
                {\n
                    DVID,   16, \n
                    Offset (0x2C),\n
                    SVID,   16, \n
                    SDID,   16, \n
                    Offset (0x4C),\n
                    WVID,   16, \n
                    WDID,   16\n
                }\n
\n
                Name (B0D1, Zero)\n
                OperationRegion (PCAP, SystemMemory, Or (PEBS, 0x8000, B0D1), 0xC0)\n
                Field (PCAP, DWordAcc, NoLock, Preserve)\n
                {\n
                    Offset (0xB0), \n
                    LCTL,   16\n
                }\n
\n
                OperationRegion (GPIO, SystemIO, 0x0500, 0x60)\n
                Field (GPIO, ByteAcc, Lock, Preserve)\n
                {\n
                    Offset (0x0C), \n
                        ,   17, \n
                    PO17,   1, \n
                    Offset (0x38), \n
                        ,   3, \n
                    PO35,   1\n
                }\n
\n
                Method (PWRE, 0, Serialized)\n
                {\n
                    Store (Zero, PO17)\n
                    Store (One, PO35)\n
                    Sleep (0x96)\n
                    Store (One, PO17)\n
                    Sleep (0x64)\n
                }\n
\n
                Method (PWRD, 0, Serialized)\n
                {\n
                    Store (Zero, PO17)\n
                    Store (Zero, PO35)\n
                }\n
\n
                Method (_ON, 0, Serialized)  // _ON_: Power On\n
                {\n
                    PWRE ()\n
                    Store (Zero, LNKD)\n
                    While (LLess (LNKS, 0x07))\n
                    {\n
                        Sleep (One)\n
                    }\n
\n
                    Store (HVID, WVID)\n
                    Store (HDID, WDID)\n
                    Or (And (ELCT, 0x43), And (LCTL, 0xFFBC), LCTL)\n
                    Notify (PEG0, Zero)\n
                }\n
\n
                Method (_OFF, 0, Serialized)  // _OFF: Power Off\n
                {\n
                    Store (LCTL, ELCT)\n
                    Store (SVID, HVID)\n
                    Store (SDID, HDID)\n
                    Store (One, LNKD)\n
                    While (LLess (LNKS, Zero))\n
                    {\n
                        Sleep (One)\n
                    }\n
\n
                    PWRD ()\n
                    Notify (PEG0, Zero)\n
                }\n
end;


 
